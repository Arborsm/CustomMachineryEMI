plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.116'
}

version = mod_version
group = mod_group_id

repositories {
    mavenCentral()
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    maven { url = "https://mvn.devos.one/snapshots" } // Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // ForgeConfigAPIPort
    maven {
        name = "Custom Machinery maven"
        url = "https://maven.frinn.fr/releases"
        content {
            includeGroup "frinn.custommachinery"
        }
    }
    maven {
        name = "Mod maven"
        url = "https://modmaven.dev/"
        content {
            includeGroup "mcjty.theoneprobe"
            includeGroup "mekanism"
        }
    }
    maven {
        name = "Shedaniel maven"
        url = "https://maven.architectury.dev/"
        content {
            includeGroup "me.shedaniel.cloth"
        }
    }
    maven {
        name = "Lat's maven"
        url = "https://maven.latvian.dev/releases"
        content {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
        content {
            includeGroup "mezz.jei"
        }
    }
    maven {
        name = 'BlameJared Maven'
        url = 'https://maven.blamejared.com'
        content {
            includeGroup "com.blamejared.crafttweaker"
            includeGroup "org.openzen.zencode"
            includeGroup "mezz.jei"
            includeGroup "com.hollingsworth.ars_nouveau"
            includeGroup "com.hollingsworth.nuggets"
            includeGroup "vazkii.patchouli"
        }
    }
    maven {
        name = "Illusive Soulworks maven"
        url = "https://maven.theillusivec4.top/"
    }
    //Needed for KubeJS
    maven {
        name = "Jitpack Maven"
        url = "https://jitpack.io"
        content {
            includeGroup "com.github.rtyley"
        }
    }
    maven {
        name = 'Forge'
        url = "https://maven.minecraftforge.net"
    }
    maven {
        name = "OctoStudios"
        url = uri("https://maven.octo-studios.com/releases")
    }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroup("software.bernie.geckolib")
        }
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    mavenLocal()
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            data()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }


dependencies {
    implementation "curse.maven:custom-machinery-457017:7090569"
    implementation "curse.maven:emi-580555:6420931"
    implementation "curse.maven:jei-238222:7129287"

    implementation "software.bernie.geckolib:geckolib-neoforge-${minecraft_version}:${geckolib_version}"
    implementation "com.hollingsworth.nuggets:nuggets-neoforge-1.21:${project.nuggets_version}"
    implementation "vazkii.patchouli:Patchouli:1.21.1-${project.patchouli_version}"
    //implementation "top.theillusivec4.curios:curios-neoforge:${project.curios_version}"
    implementation "com.hollingsworth.ars_nouveau:ars_nouveau-${project.minecraft_version}:${project.ars_version}.1214"
    implementation "curse.maven:custom-machinery-ars-nouveau-969074:6988765"

    implementation("mekanism:Mekanism:${mekanism_version}")
    implementation("mekanism:Mekanism:${mekanism_version}:generators")
    implementation "curse.maven:custom-machinery-mekanism-721079:7102843"

    implementation("com.simibubi.create:create-${minecraft_version}:${create_version}-${create_build}:slim") { transitive = false }
    implementation("net.createmod.ponder:Ponder-NeoForge-${minecraft_version}:${ponder_version}")
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${minecraft_version}:${flywheel_version}")
    runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:${flywheel_version}")
    implementation("com.tterrag.registrate:Registrate:${registrate_version}")
    implementation "curse.maven:custom-machinery-create-860747:6516361"
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [minecraft_version      : minecraft_version,
                             minecraft_version_range: minecraft_version_range,
                             neo_version            : neo_version,
                             neo_version_range      : neo_version_range,
                             loader_version_range   : loader_version_range,
                             mod_id                 : mod_id,
                             mod_name               : mod_name,
                             mod_license            : mod_license,
                             mod_version            : mod_version,
                             mod_authors            : mod_authors,
                             mod_description        : mod_description]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
